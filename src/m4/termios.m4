dnl -*- Autoconf -*-
dnl Copyright (C) 1993-2008, 2017 Free Software Foundation, Inc.
dnl This file is free software, distributed under the terms of the GNU
dnl General Public License.  As a special exception to the GNU General
dnl Public License, this file may be distributed as part of a program
dnl that contains a configuration script generated by Autoconf, under
dnl the same distribution terms as the rest of that program.

dnl From Bruno Haible, Marcus Daniels, Sam Steingold.

AC_PREREQ([2.57])

AC_DEFUN([CL_TERM],
[
  AC_BEFORE([$0], [CL_IOCTL])
  AC_CHECK_HEADERS([termios.h termio.h sys/termio.h sgtty.h])
  if test $ac_cv_header_termios_h = yes; then
    dnl HAVE_TERMIOS_H defined
    dnl A/UX has <termios.h> but is lacking tcgetattr etc.
    CL_LINK_CHECK([tcgetattr], [cl_cv_func_tcgetattr],
      [#include <termios.h>],
      [struct termios t; tcgetattr(0,&t);],
      [AC_DEFINE([HAVE_TCGETATTR],,
         [have tcgetattr(), either as a function or as a macro defined by <termios.h>])
      ])
    CL_LINK_CHECK([TCSAFLUSH in termios.h], [cl_cv_decl_TCSAFLUSH],
      [#include <termios.h>],
      [int x = TCSAFLUSH;],
      [AC_DEFINE([HAVE_TCSAFLUSH],,[<termios.h> defines TCSAFLUSH])])
    dnl Linux libc5 defines struct winsize in <termios.h>, <termio.h>, <sys/ioctl.h>.
    dnl Linux libc6 defines struct winsize in <termio.h>, <sys/ioctl.h>.
    dnl Since we don't want to include both <termios.h> and <termio.h> (they may
    dnl conflict), prefer <sys/ioctl.h> to <termio.h>.
    dnl SCO defines struct winsize in <sys/ptem.h>, which itself needs <sys/stream.h>.
    CL_COMPILE_CHECK([struct winsize in termios.h], [cl_cv_struct_winsize],
      [#include <termios.h>],
      [struct winsize w;],
      [])
    if test $cl_cv_struct_winsize = no; then
      CL_COMPILE_CHECK([struct winsize in sys/ioctl.h], [cl_cv_struct_winsize_ioctl],
        [#include <sys/types.h>
         #include <sys/ioctl.h>],
        [struct winsize w;],
        [AC_DEFINE([WINSIZE_NEED_SYS_IOCTL_H],,
           [have <termios.h> but need <sys/ioctl.h> for `struct winsize'])
        ])
      if test $cl_cv_struct_winsize_ioctl = no; then
        CL_COMPILE_CHECK([struct winsize in sys/ptem.h], [cl_cv_struct_winsize_ptem],
          [#include <sys/types.h>
           #include <sys/stream.h>
           #include <sys/ptem.h>],
          [struct winsize w;],
          [AC_DEFINE([WINSIZE_NEED_SYS_PTEM_H],,
             [have <termios.h> but need <sys/ptem.h> for `struct winsize'])
          ])
      fi
    fi
  fi
])
